buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.5.0'
    }
}

plugins {
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
    id "me.champeau.gradle.jmh" version "0.5.0"
    id "org.sonarqube" version "2.8"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'io.codearte.nexus-staging'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

scmVersion {
    tag { prefix = 'v' }
    versionCreator 'versionWithBranch'
}

project.version = scmVersion.version
project.group = 'pl.allegro.tech'

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'org.parboiled', name: 'parboiled-java', version: '1.3.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.4'

    jmh group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
}

jmh {
    jmhVersion = '1.14.1'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

nexusStaging {
    packageGroup = 'pl.allegro'
    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}

modifyPom {
    project {
        name 'opel'
        description 'Asynchronous expression language'
        url 'https://github.com/allegro/opel'
        inceptionYear '2016'

        scm {
            url 'https://github.com/allegro/opel'
            connection 'scm:git@github.com:allegro/opel.git'
            developerConnection 'scm:git@github.com:allegro/opel.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'opel-developers'
                name 'OPEL-DEVELOPERS'
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
        csv.enabled = false
        html.enabled = false
    }
}